#compdef _diraction-dispatch # -*- mode: sh   -*-
# completion for the dispatcher command

#  words: 1:function 2:directory 3:pattern

local diract=$words[2]
local dirdir=`eval echo $diract`
local subcommand=$words[3]

# Update Pwd to make subcompleter to consider we are in the target directory
OLDPWD=$PWD
PWD=$dirdir

if (( CURRENT == 3 ));
then # Subcomand completion
    case $subcommand in

        /*) # Pattern :D
            local pattern="$(echo $subcommand | sed -e 's:\(.\):\.*\1\.*:g')"
            local cmd="cd $dirdir && ls -d **/*/ | sed -E 's:^:/:g' | grep \"$pattern\" "
            local paths=("${(f)$(eval $cmd)}")
            compadd -S '' -X "Matching Subdirs" -U -a paths || _message "No matching subdir"
            # -U no to filter non matching values
            return 0
            ;;
        *)      # Basic argument
            _describe -t commands "Dispatcher subcommand" _DIRACTION_HELP_SUBCOMMAND

            compadd -x "Whitelisted commands" $DIRACTION_DISPATCH_WHITELIST
            # maybe extend subcommands with these value??
            # or extend the array here?
            return
    esac
elif [[ "$subcommand" =~ "^/." ]]; then
    dirdir="$dirdir$subcommand"
    shift words; shift words; shift words
    ((CURRENT--))
    words=(__diraction-dispatch $dirdir "$words[@]") __diraction-dispatch
    return 0
else # argument completion

    shift words; shift words  # shifting words args
    ((CURRENT = CURRENT -2))
    case $subcommand in
        e|"exec"|[-,_])  # magic native completion
            shift words; ((CURRENT--))
            if  ((CURRENT == 1 )); then
                _command_names
            elif functions "_${words[1]}" > /dev/null; then
                cd $PWD && "_${words[1]}" ; cd $OLDPWD
            else
                _path_files -W $PWD ; _options_set
            fi ; return 0
            ;;
        i|interactive|prompt|shell|help)
            _message "Just hit enter, and enter the interactive mode :)" && return 0;;

        /)
            _directories -W "$PWD"
            ;;
        *)
            local compfun="_${subcommand}"
            if functions "$compfun" > /dev/null; then
                # §tofix: git error: zsh:12: command not found: ___diraction-dispatch_main # ¤old
                # if so do it up
                cd $PWD; $compfun ; ret=$? ; cd $OLDPWD; [ $ret -eq 0 ] && return 0
                # beware to file completion:
                # https://github.com/robbyrussell/oh-my-zsh/issues/2394
            else
                _message "No Specific completion available for $subcommand"
            fi
            ;;
    esac

fi
