#compdef diraction
# Completion for the diraction command suite

local -a _1st_arguments
_1st_arguments=(
    'batch-create:Batch create diractions from stdin'
    'check-config-dir:Check which dir exist in config'
    'check-config-syntax:Check the syntax in config file'
    'check:check the config'
    'create:Create a new diraction'
    'destroy-all:Destroy all diraction'
    'destroy:Destroy specified alias'
    'disable:Disable specified Diraction'
    'enable:Enable the provided alias'
    'exist:Chec kif a diraction exist'
    'grep-alias:Look for an existig alias'
    'grep:Search in the existing diraction'
    'help:Give more details about diraction'
    'list-alias:List existing alias'
    'list-dir:List all the directories'
    'list:List the existing diraction'
    'load-config:Load the configuration'
    'reset:Reload all the diraction aliases'
)

if (( CURRENT == 2 )); then
    _describe -t commands "Diraction subcommands" _1st_arguments
    return 0
elif (( CURRENT == 3 )); then
    case $words[2] in
        create) _message "Diraction Name" && return 0;;
        destroy|disable|enable|exist) compadd -x "Existing diractions" ${(ok)DIRACTION_REGISTER} && return 0;;
        grep*) _message "Grepping pattern" && return 0;;
        *) _message "No args required"
    esac

elif (( CURRENT == 4 )); then
    case $words[2] in
        create) _path_files -/ -x "Diraction Path"  && return 0 ;;
    esac
fi
